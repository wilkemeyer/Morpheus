buildscript {
	repositories {
		jcenter()
		maven { url = "http://files.minecraftforge.net/maven" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
		classpath 'com.wynprice.cursemaven:CurseMaven:1.2.+'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'
apply plugin: 'com.wynprice.cursemaven'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

def ENV = System.getenv()
def build_number = ENV.BUILD_NUMBER ? "${ENV.BUILD_NUMBER}" : "0-custom"
version = "${mod_version_major}.${mod_version_minor}.${build_number}"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	mappings = "${mappings_version}"
	
    replace '@MAJOR@', mod_version_major
    replace '@MINOR@', mod_version_minor
    replace '@MC_VERSION@', mc_version
    replace '@BUILD_NUMBER@', build_number
}

configurations.all {
	transitive = false
}

repositories {
	mavenLocal()
	maven { url "https://maven.latmod.com/" } //FTB Library, KubeJS, Aurora
}

dependencies {
	deobfCompile "com.feed_the_beast.mods:FTBLib:${ftblib_version}"
	deobfCompile "com.feed_the_beast.mods:FTBUtilities:${ftbutilities_version}"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

artifacts {
	archives sourceJar
}

if (ENV.LOCAL_MAVEN) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "file://${ENV.LOCAL_MAVEN}")
			}
		}
	}
}

